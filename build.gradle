plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'com.lacunasoftware.amplia'
archivesBaseName = 'amplia-client'
version = '1.1.2'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
	testImplementation 'junit:junit:4.12'
}

task fatJar(type: Jar) {
    manifest { }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'amplia-client'
                packaging 'jar'
                artifactId 'amplia-client'
                description 'Classes to consume Lacuna Software Amplia'
                url 'https://github.com/LacunaSoftware/AmpliaJavaClient'

                scm {
                    connection 'scm:git:git@github.com:LacunaSoftware/AmpliaJavaClient.git'
                    developerConnection 'scm:git:git@github.com:LacunaSoftware/AmpliaJavaClient.git'
                    url 'https://github.com/LacunaSoftware/AmpliaJavaClient'
                }

                licenses {
                    license {
                        name 'Unlicense'
                    }
                }

                developers {
                    developer {
                        id 'ismaelm'
                        name 'Ismael Medeiros'
                        email 'IsmaelM@lacunasoftware.com'
                    }
                }
            }
        }
    }
}