plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'com.lacunasoftware.amplia'
archivesBaseName = 'amplia-client'
version = '1.3.0'

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs = ['src/main/java']
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.6.1'
	testImplementation 'junit:junit:4.12'
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId = 'amplia-client'
            groupId group
            version version

            // Set the POM properties
            pom {
                name = 'amplia-client'
                packaging = 'jar'
                artifactId = 'amplia-client'
                description = 'Classes to consume Lacuna Software Amplia'
                url = 'https://github.com/LacunaSoftware/AmpliaJavaClient'
                licenses {
                    license {
                        name = "Unilicense"
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:LacunaSoftware/AmpliaJavaClient.git'
                    developerConnection = 'scm:git:git@github.com:LacunaSoftware/AmpliaJavaClient.git'
                    url = 'https://github.com/LacunaSoftware/AmpliaJavaClient'
                }  
                developers {
                    developer {
                        id = "DaniloB"
                        name = "Danilo Bispo"
                        email = "danilob@lacunasoftware.com"
                    }
                    developer {
                        id = 'ismaelm'
                        name = 'Ismael Medeiros'
                        email = 'IsmaelM@lacunasoftware.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url determineRepositoryUrl()
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

task execute(type:JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'package.Main'
}

signing {
    sign publishing.publications.mavenJava
    sign configurations.archives
}


def determineRepositoryUrl() {
    def version = project.version.toString()
    if (version.endsWith('SNAPSHOT')) {
        return "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    } else {
        return "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
    }
}